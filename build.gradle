buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "nebula.ospackage" version "3.4.0"
}

task normalizeJarName(type: Copy) {
    from "${projectDir}" + "/resources/"
    into "${buildDir}" + "/libs/"
    include('openchs-server*.jar')
    rename('openchs-server.*.jar', 'openchs.jar')
}

task extractHealthModules(type: Copy) {
    from tarTree(resources.gzip("${projectDir}/resources/openchs_health_modules.tar.gz"))
    into file("${buildDir}/health_modules")
}

task extractImplSetup(type: Copy) {
    from tarTree(resources.gzip("${projectDir}/resources/openchs_impl.tar.gz"))
    into file("${buildDir}/impl")
}


group 'org.openchs'
version = project.openchsRelease

ospackage {
    packageName = 'openchs'
    release = project.buildNumber
    arch = NOARCH
    os = LINUX
    user = 'root'

    preInstall file("${projectDir}/scripts/preinstall.sh")
    postInstall file("${projectDir}/scripts/postinstall.sh")
    preUninstall file("${projectDir}/scripts/preuninstall.sh")
    postUninstall file("${projectDir}/scripts/postuninstall.sh")

    requires('postgresql94')

    into '/opt/openchs'

    from("${buildDir}/libs/openchs.jar") {
        fileMode = 0644
        createDirectoryEntry = true
        into 'lib/'
    }

    from("${projectDir}/resources/") {
        createDirectoryEntry = true
        into 'etc'
        include('openchs.conf')
    }


    from("${projectDir}/scripts/rpm/") {
        fileMode = 0744
        createDirectoryEntry = true
        into 'bin'
    }

    from("${projectDir}/resources/placeholder") {
        createDirectoryEntry = true
        into 'run'
    }

    from("${projectDir}/resources/placeholder") {
        createDirectoryEntry = true
        into 'log'
    }

    from("${projectDir}/resources/app.apk") {
        createDirectoryEntry = true
        into 'external'
    }

    from("${buildDir}/health_modules/") {
        createDirectoryEntry = true
        into 'health_modules'
    }

    from("${buildDir}/impl/") {
        createDirectoryEntry = true
        into 'impl'
    }


}

buildRpm {
    dependsOn 'normalizeJarName', 'extractHealthModules', 'extractImplSetup'
}
