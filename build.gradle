buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'org.hidetake.ssh' version '2.9.0'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

remotes {
    deploy_env {
        host = project.deploy_host
        user = project.deploy_user
        identity = file(System.properties['user.home'] + "/.ssh/id_rsa")
    }
}

task build_and_fetch_resources(type: Exec) {
    commandLine './build_and_fetch_resources.sh'
}

task collectRpms(type: Copy) {
    from {subprojects.buildRpm}
    into "$buildDir/distributions"
}
def artifactsDir = "/tmp/openchs_support/artifacts/release-${project.openchsRelease}"

task deploy {
    dependsOn 'collectRpms'
    doLast {
        ssh.run {
            session(remotes.deploy_env) {
                execute "mkdir -p $artifactsDir"
                put from: "${buildDir}/distributions/openchs-${project.openchsRelease}-${project.buildNumber}.noarch.rpm",
                        into: "$artifactsDir"
                put from: "${buildDir}/distributions/openchs-reports-${project.openchsRelease}-${project.buildNumber}.noarch.rpm",
                        into: "$artifactsDir"
                execute 'sudo service openchs stop', ignoreError: true
                execute 'sudo service openchs-reports stop', ignoreError: true
                execute 'sudo rpm -e openchs', ignoreError: true
                execute 'sudo rpm -e openchs-reports', ignoreError: true
                execute 'psql -U openchs -c \"DROP SCHEMA openchs CASCADE;\"', ignoreError: true
                execute "sudo rpm -ivh $artifactsDir/openchs-${project.openchsRelease}-${project.buildNumber}.noarch.rpm"
                execute "sudo rpm -ivh $artifactsDir/openchs-reports-${project.openchsRelease}-${project.buildNumber}.noarch.rpm"
                execute 'sudo service openchs start'
                execute 'sleep 20'
                execute 'sudo service openchs deploy_health_modules'
                execute 'sudo service openchs deploy_impl'
                execute 'sudo service openchs-reports start'
            }
        }
    }
}